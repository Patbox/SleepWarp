plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id "com.modrinth.minotaur" version "2.+"
}

repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://maven.isxander.dev/releases" }
    maven { url "https://maven.terraformersmc.com/releases/"}
    maven { url 'https://api.modrinth.com/maven' }
    maven { url = "https://maven.nucleoid.xyz/" }
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version + '+' + project.minecraft_version
group = project.maven_group

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation include("xyz.nucleoid:server-translations-api:${project.server_translation_version}")
    modImplementation include ("maven.modrinth:midnightlib:${project.midnightlib_version}-fabric")
}

loom {
    accessWidenerPath = file("src/main/resources/sleepwarp.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }


    from("src/main/resources/data/sleepwarp/lang") {
        into("assets/sleepwarp/lang")
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}


if (System.getenv("MODRINTH")) {
    modrinth {
        token = System.getenv("MODRINTH")
        projectId = 'DlSXkZVT'// The ID of your modrinth project, slugs will not work.
        versionNumber = "" + version // The version of the mod to upload.
        versionType = "beta"
        uploadFile = remapJar // This links to a task that builds your mod jar and sets "uploadFile" to the mod jar.
        gameVersions = [((String) project.minecraft_version)]
        changelog = System.getenv("CHANGELOG")
        loaders = ["fabric", "quilt"]

        dependencies {
            required.project "midnightlib"
        }
    }

    remapJar {
        finalizedBy project.tasks.modrinth
    }
}
